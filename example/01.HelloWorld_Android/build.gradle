buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.2'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"
    defaultConfig {
        applicationId "pl.grupapracuj.hermes.helloworld"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_static"
                abiFilters.addAll(["armeabi-v7a", "arm64-v8a", "x86", "x86_64"])
                cppFlags.addAll(["-std=c++17", "-frtti", "-fexceptions", "-I" + project.rootDir + "/../..", "-I" + project.rootDir + "/../../include", "-I" + project.rootDir + "/../../depend/jsoncpp/include"])
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    sourceSets {
        main {
            manifest.srcFile 'resources/AndroidManifest.xml'
            java.srcDirs = ['source/java']
            jni.srcDirs = ['source/cpp']
            resources.srcDirs = ['resources/res']
            res.srcDirs = ['resources/res']
            assets.srcDirs = ['resources/assets']
        }
    }
}

task copyHermesExt(type: Copy) {
    from "../../ext/java/"
    into "source/java/"
}
project.afterEvaluate {
    preBuild.dependsOn copyHermesExt
}
