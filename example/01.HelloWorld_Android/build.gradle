buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.1'
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 33
    buildToolsVersion "33.0.1"
    ndkVersion "25.1.8937393"
    defaultConfig {
        applicationId "pl.grupapracuj.hermes.helloworld"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_static"
                abiFilters.addAll(["armeabi-v7a", "arm64-v8a", "x86", "x86_64"])
                cppFlags.addAll(["-std=c++17", "-fPIC", "-fno-strict-aliasing", "-fstack-protector", "-fvisibility=hidden", "-fvisibility-inlines-hidden", "-frtti", "-fexceptions", "-I" + project.rootDir + "/../..", "-I" + project.rootDir + "/../../include", "-I" + project.rootDir + "/../../depend/jsoncpp/include"])
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}

task copyHermesExt(type: Copy) {
    from "../../ext/java/"
    into "src/main/java/"
}

project.afterEvaluate {
    preBuild.dependsOn copyHermesExt
}
