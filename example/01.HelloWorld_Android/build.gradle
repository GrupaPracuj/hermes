buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "pl.grupapracuj.hermes.helloworld"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_static"
                abiFilters.addAll(["armeabi-v7a", "arm64-v8a", "x86", "x86_64"])
                cppFlags.addAll(["-std=c++17", "-frtti", "-fexceptions", "-I${System.env.HERMES_HOME}", "-I${System.env.HERMES_HOME}/include", "-I${System.env.HERMES_HOME}/depend/jsoncpp/include"])
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    sourceSets {
        main {
            manifest.srcFile 'resource/AndroidManifest.xml'
            java.srcDirs = ['source/java']
            jni.srcDirs = ['source/cpp']
            resources.srcDirs = ['resource/res']
            res.srcDirs = ['resource/res']
            assets.srcDirs = ['resource/assets']
        }
    }
}

ext {
    supportLibrary = '27.0.1'
    constraintLibrary = '1.0.2'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:${supportLibrary}"
    implementation "com.android.support.constraint:constraint-layout:${constraintLibrary}"
}

task copyJavaHermesExt(type: Copy) {
    from "../../ext/java/"
    into "source/java/"
}
preBuild.dependsOn copyJavaHermesExt
