buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "pl.grupapracuj.hermes.helloworld"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_static"
                abiFilters.addAll(["armeabi-v7a", "arm64-v8a", "x86", "x86_64"])
                cppFlags.addAll(["-std=c++17", "-frtti", "-fexceptions", "-I${project.rootDir}/../..", "-I${project.rootDir}/../../include", "-I${project.rootDir}/../../depend/jsoncpp/include"])
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    sourceSets {
        main {
            manifest.srcFile 'resources/AndroidManifest.xml'
            java.srcDirs = ['source/java']
            jni.srcDirs = ['source/cpp']
            resources.srcDirs = ['resources/res']
            res.srcDirs = ['resources/res']
            assets.srcDirs = ['resources/assets']
        }
    }
}

task copyJavaHermesExt(type: Copy) {
    from "../../ext/java/"
    into "source/java/"
}
preBuild.dependsOn copyJavaHermesExt
